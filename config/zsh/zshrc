#  ┏━┓┏━┓╻ ╻┏━┓┏━╸┏━╸
#  ┗━┓┃ ┃┃ ┃┣┳┛┃  ┣╸
#  ┗━┛┗━┛┗━┛╹┗╸┗━╸┗━╸

zstyle ':zim' disable-version-check yes
zstyle ':zim:completion' dumpfile "$XDG_CACHE_HOME/.zcompdump"
zstyle ':zim:git' aliases-prefix 'g'
zstyle ':zim:input' double-dot-expand yes

source $ZIM_HOME/init.zsh
source $XDG_CONFIG_HOME/glab-cli/glab.zsh
source $XDG_CONFIG_HOME/tabtab/zsh/pnpm.zsh
source /usr/share/fzf/completion.zsh

# Use matcher-list from marlonrichert/zsh-autocomplete.
zstyle ':completion:*' matcher-list \
  'm:{[:lower:]-}={[:upper:]_} r:|[.]=**' \
  '+l:|=*'

#  ┏━┓┏━╸╺┳╸╺┳╸╻┏┓╻┏━╸┏━┓
#  ┗━┓┣╸  ┃  ┃ ┃┃┗┫┃╺┓┗━┓
#  ┗━┛┗━╸ ╹  ╹ ╹╹ ╹┗━┛┗━┛

if [[ "$TERM" = 'linux' ]]; then
  # Replicate colors definied in .Xresources for virtual console.
  color_definitions=$(sed -nr 's/\*\.color([0-9]+).*#([0-9a-fA-F]{6})/\1 \2/p' $XDG_CONFIG_HOME/Xresources)

  awk '$1 < 16 {printf "\\e]P%X%s", $1, $2}' <<<"$color_definitions"
  clear
fi

# Remove older command from the history if a duplicate is to be added.
setopt HIST_IGNORE_ALL_DUPS

# Enable vi-mode.
bindkey -v

# Remove path separator from WORDCHARS.
WORDCHARS=${WORDCHARS//[\/]}

# Enable command edit in $EDITOR.
autoload -z edit-command-line
zle -N edit-command-line

# Disable terminal close on <C-c>.
set -o ignoreeof

# Repair kill line, word and char when jumping between modes.
zle -A kill-whole-line vi-kill-line
zle -A backward-kill-word vi-backward-kill-word
zle -A backward-delete-char vi-backward-delete-char


#  ┏━╸╻ ╻┏┓╻┏━╸╺┳╸╻┏━┓┏┓╻┏━┓
#  ┣╸ ┃ ┃┃┗┫┃   ┃ ┃┃ ┃┃┗┫┗━┓
#  ╹  ┗━┛╹ ╹┗━╸ ╹ ╹┗━┛╹ ╹┗━┛

function colorize() {
  command env \
    LESS_TERMCAP_mb=$(printf "\e[1;31m") \
    LESS_TERMCAP_md=$(printf "\e[1;31m") \
    LESS_TERMCAP_me=$(printf "\e[0m") \
    LESS_TERMCAP_se=$(printf "\e[0m") \
    LESS_TERMCAP_so=$(printf "\e[1;44;33m") \
    LESS_TERMCAP_ue=$(printf "\e[0m") \
    LESS_TERMCAP_us=$(printf "\e[1;32m") "$@"
}

# Use fd in fzf completion for path listing.
function _fzf_compgen_path() {
  command fd --hidden --follow --exclude ".git" . "$1"
}

# Use fd in fzf completion for directory listing.
function _fzf_compgen_dir() {
  command fd --type d --hidden --follow --exclude ".git" . "$1"
}

# Colorize man
function man {
  colorize $0 "$@"
}

# Disable nvm "use version" check.
function nvm() {
  source /usr/share/nvm/nvm.sh --no-use
  nvm "$@"
}

# Open vifm and cd into selected directory on exit.
function vifm-cd() {
  local path="$(command vifm --choose-dir - "$@" 2> /dev/null)"

  if [[ -n "$path" ]]; then
    cd "$path"
  fi
}

# Load keybindings after vi-mode init, ensuring that they will not be
# overwritten.
function zvm_after_init() {
  bindkey -r '^T'

  source /usr/share/fzf/key-bindings.zsh
  bindkey '^F' fzf-file-widget
  bindkey '^G' fzf-cd-widget

  autopair-init

  bindkey '^ ' autosuggest-accept
  bindkey '^E' edit-command-line
  bindkey '^[^?' backward-kill-word
  bindkey '^[^H' backward-kill-word

  bindkey '^N' history-substring-search-down
  bindkey '^P' history-substring-search-up
  bindkey '^J' history-substring-search-down
  bindkey '^K' history-substring-search-up
  bindkey '^[[A' history-substring-search-up
  bindkey '^[[B' history-substring-search-down

  zmodload -F zsh/terminfo +p:terminfo
  if [[ -n ${terminfo[kcuu1]} && -n ${terminfo[kcud1]} ]]; then
    bindkey ${terminfo[kcuu1]} history-substring-search-up
    bindkey ${terminfo[kcud1]} history-substring-search-down
  fi
}

# Load normal and visual bindings after lazy load of vi-mode.
function zvm_after_lazy_keybindings() {
  bindkey -M vicmd '^F' fzf-file-widget
  bindkey -M vicmd '^G' fzf-cd-widget
  bindkey -M vicmd 'k' history-substring-search-up
  bindkey -M vicmd 'j' history-substring-search-down
}


#  ┏━╸╻ ╻┏━┓┏━┓┏━┓╺┳╸┏━┓
#  ┣╸ ┏╋┛┣━┛┃ ┃┣┳┛ ┃ ┗━┓
#  ┗━╸╹ ╹╹  ┗━┛╹┗╸ ╹ ┗━┛

export FZF_ALT_C_COMMAND='command fd --type d --hidden --follow --strip-cwd-prefix --exclude .git'
export FZF_ALT_C_OPTS="--preview 'exa -T -L 1 --group-directories-first --color=always {}'"
export FZF_CTRL_T_COMMAND='command fd --hidden --follow --strip-cwd-prefix --exclude .git'
export FZF_CTRL_T_OPTS="--preview 'bat --color=always {} 2> /dev/null || exa -T -L 1 --group-directories-first --color=always {}'"
export FZF_DEFAULT_COMMAND='command fd --hidden --follow --strip-cwd-prefix --exclude .git'
export FZF_DEFAULT_OPTS="--bind ctrl-a:toggle-all,ctrl-b:page-up,ctrl-d:half-page-down,ctrl-f:page-down,ctrl-u:half-page-up --color=bg+:#282828,spinner:#b16286,hl:#b16286 --color=fg:#ebdbb2,info:#458588,pointer:#98971a --color=marker:#98971a,hl+:#98971a --reverse --tiebreak=length,end --preview 'bat --color=always {} 2> /dev/null || exa -T -L 1 --group-directories-first --color=always {} 2> /dev/null || echo {}' --preview-window 'down:50%'"


#  ┏━┓╻  ╻┏━┓┏━┓┏━╸┏━┓
#  ┣━┫┃  ┃┣━┫┗━┓┣╸ ┗━┓
#  ╹ ╹┗━╸╹╹ ╹┗━┛┗━╸┗━┛

# Safety.
alias chgrp='chgrp --preserve-root -v'
alias chmod='chmod --preserve-root -v'
alias chown='chown --preserve-root -v'
alias cp='cp -ir'
alias ln='ln -i'
alias mv='mv -i'
alias rm='rm -I --preserve-root'

# Goodies & Misc.
alias cmd=command
alias df='df -h'
alias du='du -ch'
alias dud='du -chd'
alias free='free -m'
alias ga='git-alias-lookup $XDG_DATA_HOME/zim/modules/git'
alias get='curl --continue-at - --location --progress-bar --remote-name --remote-time'
alias grep='grep --color=auto -d skip'
alias mkdir='mkdir -pv'
alias more=less
alias np='nvim -w PKGBUILD'
alias o=open
alias open='mimeo -q'
alias q=exit
alias sudo='sudo '
alias sudoedit='sudoedit '
alias s=sudo
alias se=sudoedit

# Globals.
alias -g CP='| xsel -ib'
alias -g F='| fzf'
alias -g V='| nvim'

# Listing.
alias exa='exa --group-directories-first --color=auto'
alias la='exa -la'
alias ll='exa -l'
alias ls='exa'
alias lt='exa -la -T'

# State.
alias i3exit='state exit'
alias poweroff='state poweroff'
alias reboot='state reboot'
alias shutdown='state shutdown'
alias suspend='state suspend'

# Apps.
alias bluetooth=bluetoothctl
alias c=cat
alias cheat='cht.sh'
alias chrome=google-chrome-stable
alias fd='fd --hidden --follow'
alias feh='feh --scale-down --auto-zoom --image-bg "#282828"'
alias fm=vifm-cd
alias format-opinionated='npx prettier --single-quote --trailing-comma es5 --write'
alias format='npx prettier --write'
alias glab='BROWSER=google-chrome-stable glab'
alias kubectl="kubectl --cache-dir $XDG_CACHE_HOME/kube"
alias l=less
alias mixer=pavucontrol
alias qutebrowser=open-qutebrowser
alias rcup="$SCRIPTS/extended-rcup.sh"
alias rg='rg --hidden --follow'
alias t=term
alias td="term --detach"
alias tiga='tig --all'
alias v=nvim
alias vi=nvim
alias vifm=vifm-cd
alias vim=nvim
alias vimn='nvim -u NONE'
alias xsel='xsel -b'

# Dunst.
alias dunst-pause='dunstctl set-paused true'
alias dunst-resume='dunstctl set-paused false'

# Todo.
alias todo='note todo'
